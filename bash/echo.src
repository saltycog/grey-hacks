// command: echo

// --- Global Functions ------------------------------
get_params_before_pipe = function()
    return params.join(" ").split("\|")[0].split(" ")
end function

get_piped_program = function()
    pipe_tokens = params.join(" ").split("\|")
    if pipe_tokens.len > 1 then 
        program = pipe_tokens[1:].join("|").trim
        program_path = program
        if program_path.indexOf("/") != 0 then program_path = current_path + "/" + program
        computer = get_shell.host_computer
        program_file = computer.File(program_path)
        if program_file == null then program_path = "/bin/" + program
        program_file = computer.File(program_path)
        if program_file != null then return program_path
    end if
    return null
end function

launch_piped_program = function(args)
    args = args.replace("\\n", char(10))
    pipe_program = get_piped_program
    if pipe_program != null then 
        launch(get_shell, pipe_program, args)
        return true
    end if
    return false
end function

// --- MAIN ------------------------------------------

output = get_params_before_pipe.join(" ").replace("""", "")

if not launch_piped_program(output) then
    print(output)
end if